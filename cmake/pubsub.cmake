option(LOTTO_PS_INTERNAL
       "list of pubsub topics using the pubsub interface to be made internal"
       OFF)

option(LOTTO_PS_INTERNAL_ALL
       "Changes all pub sub interactions to be internal function calls." OFF)

set(CALLEE_PROCESSED
    ""
    CACHE INTERNAL "pubsub" FORCE)

function(add_local_callee)
    if("${CMAKE_CURRENT_LIST_DIR}" IN_LIST CALLEE_PROCESSED)
        return()
    endif()

    list(APPEND CALLEE_PROCESSED "${CMAKE_CURRENT_LIST_DIR}")
    set(CALLEE_PROCESSED
        ${CALLEE_PROCESSED}
        CACHE INTERNAL "pubsub" FORCE)

    file(RELATIVE_PATH PATH_REL ${CMAKE_SOURCE_DIR} ${CMAKE_CURRENT_LIST_DIR})
    if(PATH_REL MATCHES "^src/"
       OR PATH_REL MATCHES "^test/"
       OR PATH_REL MATCHES "^plugins/")
        # message("Local rel dir: ${PATH_REL}")
        file(GLOB_RECURSE SRC_C ${CMAKE_CURRENT_LIST_DIR} *.c)

        foreach(FILE_C ${SRC_C})
            get_filename_component(DEF_NAME ${FILE_C} NAME_WLE)
            file(RELATIVE_PATH PATH_REL ${CMAKE_SOURCE_DIR} ${FILE_C})
            get_filename_component(FILE_PATH ${PATH_REL} DIRECTORY)

            string(REPLACE "/" "_" FILE_PATH "${FILE_PATH}")
            string(REPLACE "." "_" FILE_PATH "${FILE_PATH}")
            string(REPLACE "-" "_" FILE_PATH "${FILE_PATH}")
            string(REPLACE " " "_" FILE_PATH "${FILE_PATH}")

            string(REPLACE "." "_" DEF_NAME "${DEF_NAME}")
            string(REPLACE "-" "_" DEF_NAME "${DEF_NAME}")
            string(REPLACE " " "_" DEF_NAME "${DEF_NAME}")

            set(CALLEE_NAME ${FILE_PATH}_${DEF_NAME})
            # message("File: ${FILE_C} with -DLOCAL_CALLEE=${CALLEE_NAME}")
            set_property(
                SOURCE ${FILE_C}
                APPEND
                PROPERTY COMPILE_DEFINITIONS "LOCAL_CALLEE=${CALLEE_NAME}")
        endforeach()

    endif()
endfunction()

function(add_executable _name)
    _add_executable(${ARGV})
    add_local_callee()
endfunction()

function(add_library _name)
    _add_library(${ARGV})
    add_local_callee()
endfunction()

function(gen_pubsub_headers src_dir build_dir)
    # generate autogenerated headers
    set(GENERATED_DIR "${build_dir}/generated/lotto/brokers/pubsub")
    set(HEADER_INTERNAL "${GENERATED_DIR}/pubsub_internal_autogen.h")
    set(HEADER_CALLEES "${GENERATED_DIR}/pubsub_callee_autogen.h")

    if(NOT ("${LOTTO_PS_INTERNAL}" STREQUAL "OFF" AND "${LOTTO_PS_INTERNAL_ALL}"
                                                      STREQUAL "OFF"))
        add_compile_definitions(LOTTO_PUBSUB_INTERFACE)
        if(EXISTS "${HEADER_CALLEES}" AND EXISTS "${HEADER_INTERNAL}")
            return()
        endif()

        if(EXISTS "${HEADER_CALLEES}")
            file(REMOVE "${HEADER_CALLEES}")
        endif()

        if(EXISTS "${HEADER_INTERNAL}")
            file(REMOVE "${HEADER_INTERNAL}")
        endif()

        make_directory("${GENERATED_DIR}")
        execute_process(
            COMMAND bash ${src_dir}/scripts/gen_ps_topics.sh ${src_dir}
                    ${HEADER_CALLEES} OUTPUT_VARIABLE PS_INTERFACE_TOPICS_ALL)

        set(LOTTO_PS_EXTERNAL "")

        if(NOT ("${LOTTO_PS_INTERNAL_ALL}" STREQUAL "OFF"))
            set(LOTTO_PS_INTERNAL "")
            list(APPEND LOTTO_PS_INTERNAL ${PS_INTERFACE_TOPICS_ALL})
        else()
            list(APPEND LOTTO_PS_EXTERNAL ${PS_INTERFACE_TOPICS_ALL})
            string(REPLACE "," ";" LOTTO_PS_INTERNAL "${LOTTO_PS_INTERNAL}")
            list(REMOVE_ITEM LOTTO_PS_EXTERNAL ${LOTTO_PS_INTERNAL})
        endif()

        string(REPLACE ";" "," PS_IN "${LOTTO_PS_INTERNAL}")
        string(REPLACE ";" "," PS_EX "${LOTTO_PS_EXTERNAL}")

        execute_process(
            COMMAND bash ${src_dir}/scripts/gen_ps_internal.sh
                    ${HEADER_INTERNAL} "${PS_IN}" "${PS_EX}"
            OUTPUT_VARIABLE PS_INTERFACE_INTERNAL)
    endif()
endfunction()
